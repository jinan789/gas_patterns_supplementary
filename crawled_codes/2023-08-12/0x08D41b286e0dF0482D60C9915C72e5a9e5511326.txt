//SPDX-License-Identifier: CC-BY-NC-ND-2.5
pragma solidity 0.8.16;

import {ERC20Burnable} from "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";
import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {Pausable} from "@openzeppelin/contracts/security/Pausable.sol";
import {ERC20, ERC20Permit} from "@openzeppelin/contracts/token/ERC20/extensions/ERC20Permit.sol";
import {ERC20Votes} from "@openzeppelin/contracts/token/ERC20/extensions/ERC20Votes.sol";
import {ERC20Snapshot} from  "@openzeppelin/contracts/token/ERC20/extensions/ERC20Snapshot.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";


/**
 * @title JubiERC20Impl
 * @notice This contract is used as a template for  ERC20 tokens in the jubi.io ecosystem.
 * @dev The intention is for a new version to be deployed from the Jubi factory, with the appropriate values.
 */
contract JubiERC20Impl is
    ERC20,
    Ownable,
    ERC20Burnable,
    Pausable,
    ERC20Permit,
    ERC20Snapshot,
    ERC20Votes
{
    using SafeERC20 for ERC20;

    /// @notice Accounts which are granted access to mint/burn tokens
    mapping(address => bool) public minters;

    event MinterSet(address indexed account, bool canMint);
    event TokenRecovered(address indexed tokenAddress, address indexed toAddress, uint256 tokenAmount);

    /**
     * @notice The ERC20 token constructor with a new name and symbol.
     * @param name_ The name of the token.
     * @param symbol_ The symbol of the token.
     */
    constructor(string memory name_, string memory symbol_)
        ERC20(name_, symbol_) 
        Ownable() 
        ERC20Permit(name_)
    {
        _pause();
    }

    /**
     * @notice Pauses token transfers and user burns.
     * @dev Owner still has rights to transfer/burn tokens while paused.
     */
    function pause() public onlyOwner {
        _pause();
    }

    /**
     * @notice Unpauses token transfers and user burns.
     */
    function unpause() public onlyOwner {
        _unpause();
    }

    /**
     * @notice Add accounts which are allowed to mint new tokens.
     */
    function setMinter(address account, bool canMint) external onlyOwner {
        minters[account] = canMint;
        emit MinterSet(account, canMint);
    }

    /**
     * @notice Mints new tokens to the specified address.
     * @dev This function can only be called by the Minters
     * @param to The address to send the new tokens to.
     * @param amount The amount of new tokens to mint.
     */
    function mint(address to, uint256 amount) public onlyMinter {
        _mint(to, amount);
    }

    /**
     * @notice Burns tokens from the specified account.
     * @dev This function can only be called by the Minter
     * @param account The account to burn tokens from.
     * @param amount The amount of tokens to burn.
     */
    function burnFrom(address account, uint256 amount) public override onlyMinter {
        _burn(account, amount);
    }

    /**
     * @notice Burns tokens from the sender's account if the token is not paused.
     * @param amount The amount of tokens to burn.
     */
    function burn(uint256 amount) public override whenNotPaused {
        _burn(msg.sender, amount);
    }

    /**
     * @notice If the token is paused, only the Owner can transfer tokens.
     * @dev Overrides the _beforeTokenTransfer function from the ERC20 and ERC20Snapshot contracts.
     * @param from The address to transfer tokens from.
     * @param to The address to transfer tokens to.
     * @param amount The amount of tokens to transfer.
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal override(ERC20, ERC20Snapshot) {
        // If paused, the owner can still transfer their tokens.
        require(!paused() || msg.sender == owner(), "Pausable: paused");

        super._beforeTokenTransfer(from, to, amount);
    }

    /**
     * @dev Recovers ERC20 tokens sent to the contract.
     * This function can only be called by the Owner.
     * @param tokenAddress The address of the ERC20 token to recover.
     * @param toAddress The address to send the recovered ERC20 tokens to.
     * @param tokenAmount The amount of ERC20 tokens to recover.
     */
    function recoverERC20(
        address tokenAddress,
        address toAddress,
        uint256 tokenAmount
    ) public onlyOwner {
        ERC20(tokenAddress).safeTransfer(toAddress, tokenAmount);
        emit TokenRecovered(tokenAddress, toAddress, tokenAmount);
    }

    // The following functions are overrides required by Solidity.
    /**
     * @dev Overrides the _afterTokenTransfer function from the ERC20 and ERC20Votes contracts.
     * @param from The address to transfer tokens from.
     * @param to The address to transfer tokens to.
     * @param amount The amount of tokens to transfer.
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal override(ERC20, ERC20Votes) {
        super._afterTokenTransfer(from, to, amount);
    }

    /**
     * @dev Overrides the _mint function from the ERC20 and ERC20Votes contracts.
     * @param to The address to send the new tokens to.
     * @param amount The amount of new tokens to mint.
     */
    function _mint(
        address to,
        uint256 amount
    ) internal override(ERC20, ERC20Votes) {
        super._mint(to, amount);
    }

    /**
     * @dev Overrides the _burn function from the ERC20 and ERC20Votes contracts.
     * @param account The account to burn tokens from.
     * @param amount The amount of tokens to burn.
     */
    function _burn(
        address account,
        uint256 amount
    ) internal override(ERC20, ERC20Votes) {
        super._burn(account, amount);
    }

    function _requireMinter() internal view {
        require(minters[msg.sender]|| msg.sender == owner(), "Only Minters");
    }

    modifier onlyMinter() {
        _requireMinter();
        _;
    }
}