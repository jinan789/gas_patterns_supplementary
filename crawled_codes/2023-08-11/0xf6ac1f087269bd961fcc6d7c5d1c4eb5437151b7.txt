// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.0;

import {IPancakeSwapVault, IPancakeV3Pool, IPancakeNonfungiblePositionManager} from "@mellow-vaults/contracts/interfaces/vaults/IPancakeSwapVault.sol";
import {OracleLibrary} from "@mellow-vaults/contracts/interfaces/external/pancakeswap/libraries/OracleLibrary.sol";

import "../../../libraries/external/FullMath.sol";

import "./IMEVProtectionImpl.sol";

contract PancakeVaultMEVProtection is IMEVProtectionImpl {
    error NotEnoughObservations();
    error LimitOverflow();

    uint256 public constant D9 = 10 ** 9;

    IPancakeNonfungiblePositionManager public immutable positionManager;
    uint256 public immutable maxDeviationD;
    int24 public immutable maxAbsoluteDeviation;
    uint32 public immutable timespan;

    constructor(
        IPancakeNonfungiblePositionManager positionManager_,
        uint256 maxDeviationD_,
        int24 maxAbsoluteDeviation_,
        uint32 timespan_
    ) {
        positionManager = positionManager_;
        maxDeviationD = maxDeviationD_;
        maxAbsoluteDeviation = maxAbsoluteDeviation_;
        timespan = timespan_;
    }

    function ensureNoMEV(address vault) external view {
        uint256 uniV3Nft = IPancakeSwapVault(vault).uniV3Nft();
        if (uniV3Nft == 0) return;
        IPancakeV3Pool pool = IPancakeV3Pool(address(IPancakeSwapVault(vault).pool()));
        (, , , , , int24 tickLower, int24 tickUpper, , , , , ) = positionManager.positions(uniV3Nft);
        int24 maxDeviation = int24(uint24(FullMath.mulDiv(uint24(tickUpper - tickLower), maxDeviationD, D9)));
        if (maxDeviation > maxAbsoluteDeviation) {
            maxDeviation = maxAbsoluteDeviation;
        }
        (, int24 spotTick, , , , , ) = pool.slot0();
        (int24 arithmeticMeanTick, , bool withFail) = OracleLibrary.consult(address(pool), timespan);
        if (withFail) revert NotEnoughObservations();
        int24 deviation = arithmeticMeanTick - spotTick;
        if (deviation < 0) deviation = -deviation;
        if (deviation > maxDeviation) revert LimitOverflow();
    }
}